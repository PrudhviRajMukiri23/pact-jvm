plugins {
  id 'au.com.dius.pact.kotlin-common-conventions'
}

configurations {
  cucumberRuntime {
    extendsFrom testImplementation
  }
}

dependencies {
  testImplementation 'io.cucumber:cucumber-java:7.12.0'
  testImplementation 'io.cucumber:cucumber-picocontainer:7.12.0'
  testImplementation 'org.apache.groovy:groovy'
  testImplementation 'org.apache.groovy:groovy-json'
  testImplementation project(':core:model')
  testImplementation project(':core:matchers')
  testImplementation project(':consumer')
  testImplementation project(':provider')
  testImplementation('io.pact.plugin.driver:core') {
    exclude group: 'au.com.dius.pact.core'
  }
  testImplementation 'ch.qos.logback:logback-classic'
  implementation 'io.ktor:ktor-http-jvm'
}

tasks.register('v1') {
  dependsOn assemble, testClasses
  doLast {
    def cucumberArgs = [
      '--plugin', 'pretty',
      '--plugin', 'html:build/cucumber-report-v1.html',
      '--glue', 'steps.v1',
      'pact-compatibility-suite/features/V1'
    ]
    if (project.hasProperty('cucumber.filter.tags')) {
      cucumberArgs.add(0, project.property('cucumber.filter.tags'))
      cucumberArgs.add(0, '-t')
    }
    javaexec {
      main = "io.cucumber.core.cli.Main"
      classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
      args = cucumberArgs
      systemProperty 'pact_do_not_track', 'true'
    }
  }
}
